# * ***************************************************************************
# *
# *  Copyright (C) 2017 University of Dundee
# *  All rights reserved. 
# *
# *  This file is part of RheoVM (Rheology of Vertex Model) program.
# *
# *  RheoVM is free software; you can redistribute it and/or modify
# *  it under the terms of the GNU General Public License as published by
# *  the Free Software Foundation; either version 2 of the License, or
# *  (at your option) any later version.
# *
# *  RheoVM is distributed in the hope that it will be useful,
# *  but WITHOUT ANY WARRANTY; without even the implied warranty of
# *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# *  GNU General Public License for more details.
# *
# *  You should have received a copy of the GNU General Public License
# *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
# *
# * ****************************************************************************/

find_package(VTK)
if(VTK_FOUND)
  if (VTK_MAJOR_VERSION LESS 9)
    include(${VTK_USE_FILE})
  endif()
  add_definitions(-DHAS_VTK)
  set(VTK_LIBS ${VTK_LIBRARIES})
  set(VTK_Group_StandAlone OFF)
  set(VTK_Group_Rendering OFF)
else(VTK_FOUND)
  message("VTK not found on the system. It will be downloaded in compiled locally. It may take a while.")
  include(ExternalProject)
  set(EXTERNAL_INSTALL_LOCATION ${CMAKE_BINARY_DIR}/extern/VTK)
  set(VTK_version 9.0)
  ExternalProject_Add(VTKLocal
    GIT_REPOSITORY https://github.com/Kitware/VTK.git
    GIT_TAG v${VTK_version}.0
    GIT_SHALLOW TRUE
    CMAKE_ARGS -DCMAKE_INSTALL_PREFIX=${EXTERNAL_INSTALL_LOCATION} 
	       -DCMAKE_INSTALL_LIBDIR=${EXTERNAL_INSTALL_LOCATION}/lib
               -DCMAKE_BUILD_TYPE=Debug
               -DBUILD_EXAMPLES=OFF 
               -DBUILD_SHARED_LIBS=ON 
               -DBUILD_TESTING=OFF 
               -DVTK_Group_Rendering=OFF 
               -DVTK_Group_StandAlone=OFF 
               -DVTK_WRAP_PYTHON=OFF 
               -DModule_vtkIOXML=ON 
               -DModule_vtkIOXMLParser=ON 
               -DVTK_RENDERING_BACKEND=None
   )
  set(VTKLocal TRUE)
  set(VTK_LIBS vtkCommonCore-${VTK_version} 
               vtkCommonDataModel-${VTK_version}
               vtkCommonExecutionModel-${VTK_version} 
               vtkCommonMath-${VTK_version} 
               vtkCommonSystem-${VTK_version}
               vtkCommonTransforms-${VTK_version}
               vtkIOCore-${VTK_version}
               vtkIOXML-${VTK_version}
               vtkIOXMLParser-${VTK_version}
               vtkdoubleconversion-${VTK_version}
               vtkexpat-${VTK_version}
               vtklz4-${VTK_version}
               vtklzma-${VTK_version}
               vtksys-${VTK_version}
               vtkzlib-${VTK_version}
               )
  include_directories(${EXTERNAL_INSTALL_LOCATION}/include/vtk-${VTK_version})
  link_directories(${EXTERNAL_INSTALL_LOCATION}/lib)
  # use, i.e. don't skip the full RPATH for the build tree
  set(CMAKE_SKIP_BUILD_RPATH FALSE)

  # when building, don't use the install RPATH already
  # (but later on when installing)
  set(CMAKE_BUILD_WITH_INSTALL_RPATH FALSE)

  set(CMAKE_INSTALL_RPATH "${EXTERNAL_INSTALL_LOCATION}/lib")

  # add the automatically determined parts of the RPATH
  # which point to directories outside the build tree to the install RPATH
  set(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)

  # the RPATH to be used when installing, but only if it's not a system directory
  list(FIND CMAKE_PLATFORM_IMPLICIT_LINK_DIRECTORIES "${EXTERNAL_INSTALL_LOCATION}/lib" isSystemDir)
  if("${isSystemDir}" STREQUAL "-1")
          set(CMAKE_INSTALL_RPATH "${EXTERNAL_INSTALL_LOCATION}/lib")
  endif("${isSystemDir}" STREQUAL "-1")
  add_definitions(-DHAS_VTK)
endif(VTK_FOUND)
